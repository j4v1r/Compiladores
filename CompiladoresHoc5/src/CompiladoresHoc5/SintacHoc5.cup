package CompiladoresHoc5;
import java_cup.runtime.*;
import java.io.FileReader;

class AnalizadorSintactico;

parser code{:
    formHoc5 frmInterfaz;
    MaquinaHoc4 maquinaHoc4;

    public void report_error(String mensaje,Object info){
        StringBuilder m = new StringBuilder("Error");
        if(info instanceof java_cup.runtime.Symbol){
            java_cup.runtime.Symbol s = (java_cup.runtime.Symbol) info;
            if(s.left >=0){
                m.append(" en la linea " + (s.left+1));
                if(s.right>=0)
                    m.append(", columna " + (s.right+1));
            }
        }
        m.append(" : " + mensaje);
        System.err.println(m);
    }   

    public void report_fatal_error(String message, Object info){
        report_error(message, info);
        System.exit(1);
    }
:}

terminal SEMIC;
terminal OpSuma, OpResta, OpProd, OpDiv, OpAsig, OpPotencia;
terminal ParIzq, ParDer;
terminal LlaveIzq, LlaveDer;
terminal PRINT, WHILE, IF, ELSE;
terminal SymbolHoc NUM;
terminal SymbolHoc VAR;
terminal SymbolHoc CONST_PRED; /* PI , E, PHI , RAD, etc*/
terminal SymbolHoc BLTIN; /* sin,cos,atan,etc*/
terminal MENOSUNARIO;
terminal OR, AND, GT, GE, LT, LE, EQ, NE, NOT;

non terminal list;
non terminal Integer asgn, expr, stmt, stmtlist, if, end, cond, endcondif, endif, endcondwhile, endwhile; /*Casilla donde empieza su c√≥digo*/

precedence nonassoc IF;
precedence nonassoc ELSE;
precedence right OpAsig;
precedence left OR;
precedence left AND;
precedence left GT, GE, LT, LE, EQ, NE;
precedence left OpSuma, OpResta;
precedence left OpProd, OpDiv;
precedence left MENOSUNARIO, NOT;
precedence right OpPotencia;

list    ::=
        | list stmt                         {:
                                                /*InstrucPrograma ins1 =  new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.PRINT;

                                                InstrucPrograma ins2 =  new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins2.Instruc = EnumInstrMaq.STOP;

                                                maquinaHoc4.code2(ins1,ins2);*/
                                                /*frmInterfaz.txtResultSintactico.append(Float.toString(valExpr) + "\n");*/
                                            :}
        ;
asgn    ::= VAR:v OpAsig expr:ind1          {:  InstrucPrograma ins1 =  new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.VARPUSH;

                                                InstrucPrograma ins2 =  new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.SYMBOL;
                                                ins2.symbolHoc = v;
                                                
                                                InstrucPrograma ins3 =  new InstrucPrograma();
                                                ins3.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins3.Instruc = EnumInstrMaq.ASSIGN;

                                                maquinaHoc4.code3(ins1,ins2,ins3);
                                                RESULT = ind1;
                                            :}
        ;

stmt    ::= asgn:ind1 SEMIC                 {:  RESULT = ind1;
                                            :}

        |   PRINT expr:ind1 SEMIC           {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.PRINT;
                                                
                                                maquinaHoc4.code(ins1); 
                                                RESULT = ind1;
                                            :}

        |   WHILE cond:ind1 endcondwhile:ind2 stmt:ind3 endwhile:ind4
                                           {:  /*Fin de while*/
                                               InstrucPrograma ins1 = new InstrucPrograma();
                                               ins1.TipInstr = EnumTipoInstr.JUMP;
                                               ins1.jump = ind4+1;
                                               maquinaHoc4.Prog[ind2+1] = ins1;

                                               /*Evalua de nuevo cond para seguir ciclo*/
                                               InstrucPrograma ins2 = new InstrucPrograma();
                                               ins2.TipInstr = EnumTipoInstr.JUMP;
                                               ins2.jump = ind1;
                                               maquinaHoc4.Prog[ind4] = ins2;

                                               RESULT = ind1;
                                           :}

        |   if                              

        |   LlaveIzq stmtlist:ind LlaveDer  {:  RESULT = ind;
                                            :}
        ;

if      ::= IF cond:ind1 endcondif:ind2 stmt:ind3 endif:ind4
                                           {:   System.out.println("IF-NORMAL");
                                                /*Fin del if*/
                                                InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.JUMP;
                                                ins1.jump = ind4;
                                                maquinaHoc4.Prog[ind2+2] = ins1;
                                                
                                                /*STOP para ELSE, hace salto a JUMPFALSE*/
                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.JUMP;
                                                ins2.jump = ind2+3;
                                                maquinaHoc4.Prog[ind2+1] = ins2;

                                                RESULT = ind1;
                                            :}
%prec IF
        |   IF cond:ind1 endcondif:ind2 stmt:ind3 endif:ind4 ELSE stmt:ind5 endif:ind6      
                                            {:  System.out.println("IF-ELSE");
                                                /*STOP para ELSE, hace salto a stmt de else*/
                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.JUMP;
                                                ins2.jump = ind5;
                                                maquinaHoc4.Prog[ind2+1] = ins2;

                                                /*Fin del if-else*/
                                                InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.JUMP;
                                                ins1.jump = ind6;
                                                maquinaHoc4.Prog[ind2+2] = ins1;

                                                RESULT = ind1;
                                            :}
        ;

cond    ::= ParIzq expr:ind1 ParDer         {:  RESULT = ind1;
                                            :}
        ;

endcondwhile::=                             {: int auxProgp;

                                               InstrucPrograma ins1 = new InstrucPrograma();
                                               ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                               ins1.Instruc = EnumInstrMaq.JUMPFALSE;

                                               InstrucPrograma ins2 = new InstrucPrograma();
                                               ins2.TipInstr = EnumTipoInstr.JUMP;
                                               ins2.jump = -1;
                                                    
                                               auxProgp = maquinaHoc4.progp;
                                               maquinaHoc4.code2(ins1,ins2);

                                               RESULT = auxProgp;
                                            :}
        ;

endwhile ::=                               {:  int auxProgp;

                                               InstrucPrograma ins1 = new InstrucPrograma();
                                               ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                               ins1.Instruc = EnumInstrMaq.STOP;

                                               auxProgp = maquinaHoc4.progp;
                                               maquinaHoc4.code(ins1);

                                               RESULT = auxProgp;
                                           :}
        ;

endcondif::=                               {:   int auxProgp;

                                                InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.JUMP;
                                                ins1.jump = -1;

                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins2.Instruc = EnumInstrMaq.JUMPTRUE;

                                                InstrucPrograma ins3 = new InstrucPrograma();
                                                ins3.TipInstr = EnumTipoInstr.JUMP;
                                                ins3.jump = -1;
                                                    
                                                auxProgp = maquinaHoc4.progp;
                                                maquinaHoc4.code3(ins2,ins1,ins3);

                                                RESULT = auxProgp;
                                            :}
        ;

endif   ::=                                 {:  RESULT = maquinaHoc4.progp;
                                            :}
        ;

end     ::=                                 {:  InstrucPrograma ins1 =  new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.STOP;
                                                maquinaHoc4.code(ins1);
                                                
                                                RESULT = maquinaHoc4.progp;
                                            :}
        ;

stmtlist::= stmt:ind                        {:  RESULT = ind;
                                            :}
        | stmtlist:ind stmt                 {:  RESULT = ind;   
                                            :}
        ;

expr    ::= NUM:n                           {:  InstrucPrograma ins1 =  new InstrucPrograma(); 
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.CONSTPUSH;

                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.SYMBOL;
                                                ins2.symbolHoc = n;

                                                RESULT = maquinaHoc4.code2(ins1,ins2);
                                            :}

        |   VAR:v                           {:  InstrucPrograma ins1 =  new InstrucPrograma(); 
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.VARPUSH;

                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.SYMBOL;
                                                ins2.symbolHoc = v;

                                                InstrucPrograma ins3 = new InstrucPrograma();
                                                ins3.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins3.Instruc = EnumInstrMaq.EVAL;

                                                RESULT = maquinaHoc4.code3(ins1, ins2, ins3);
                                            :}

        |   CONST_PRED:n                    {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.CONSTPUSH;

                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.SYMBOL;
                                                ins2.symbolHoc = n;
                                                
                                                RESULT = maquinaHoc4.code2(ins1, ins2);
                                            :}

        |   asgn:ind                        {:  RESULT = ind; :}

        |   expr:ind1 OpSuma expr           {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.ADD;
                                                
                                                maquinaHoc4.code(ins1);
                                                RESULT = ind1;
                                            :}

        |   expr:ind1 OpResta expr          {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.SUB;
                                                
                                                maquinaHoc4.code(ins1);
                                                RESULT = ind1;
                                            :}

        |   expr:ind1 OpProd expr           {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.MUL;

                                                maquinaHoc4.code(ins1);
                                                RESULT = ind1;
                                            :}

        |   expr:ind1 OpDiv expr            {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.DIV;
                                                
                                                maquinaHoc4.code(ins1);
                                                RESULT = ind1;
                                            :}

        |   ParIzq expr:ind ParDer          {:  RESULT = ind;  :}

        |   BLTIN:v ParIzq expr:ind1 ParDer {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.BLTIN;

                                                InstrucPrograma ins2 = new InstrucPrograma();
                                                ins2.TipInstr = EnumTipoInstr.BLTIN;
                                                ins2.Func_BLTIN = v.FuncPredef;

                                                maquinaHoc4.code2(ins1, ins2);
                                                RESULT = ind1;
                                            :}

        |   OpResta expr:ind1               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.NEGATE;

                                                maquinaHoc4.code(ins1);
                                                RESULT = ind1;
                                            :}
                                    %prec MENOSUNARIO

        |   expr:ind1 OpPotencia expr       {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.POWER;

                                                maquinaHoc4.code(ins1);
                                                RESULT = ind1;
                                            :}

        |   expr:ind1 GT expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.GT;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 GE expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.GE;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 LT expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.LT;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 LE expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.LE;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 EQ expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.EQ;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 NE expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.NE;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 AND expr              {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.AND;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}

        |   expr:ind1 OR expr               {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.OR;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}
   
        |   NOT expr:ind1                   {:  InstrucPrograma ins1 = new InstrucPrograma();
                                                ins1.TipInstr = EnumTipoInstr.INSTRUC;
                                                ins1.Instruc = EnumInstrMaq.NOT;
                                                maquinaHoc4.code(ins1);

                                                RESULT = ind1;
                                            :}
        ;